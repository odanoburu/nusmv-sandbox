MODULE main
 VAR
   -- decide which thing farmer will take with him, if anything
   takes  : {over, nothing, cabbage, lamb, wolf} ;
   cabbage-over : boolean ;
   lamb-over : boolean ;
   wolf-over : boolean ;
   farmer-over : boolean ;
 ASSIGN
   -- takes nothing in the first round
   init(takes) := nothing ;
   -- everyone starts not-over the river
   init(cabbage-over) := FALSE ;
   init(lamb-over) := FALSE ;
   init(wolf-over) := FALSE ;
   init(farmer-over) := FALSE ;
   -- what the farmer takes with him
   next(takes) := case
                    -- everyone over the river
                    farmer-over = TRUE & farmer-over = cabbage-over & farmer-over = lamb-over & farmer-over = wolf-over : over ;
                    -- c,l,w -> l
                    farmer-over = cabbage-over & farmer-over = lamb-over & farmer-over = wolf-over : lamb ;
                    -- c,l -> c,l
                    farmer-over = cabbage-over & farmer-over = lamb-over : {cabbage,lamb} ;
                    -- c,w -> c,w,-1
                    farmer-over = cabbage-over & farmer-over = wolf-over : {nothing,cabbage,wolf} ;
                    -- l,w -> l,w
                    farmer-over = lamb-over & farmer-over = wolf-over : {lamb,wolf} ;
                    -- l -> {l,nothing}
                    farmer-over = lamb-over : {nothing,lamb} ;
                    -- w -> WRONG or c -> WRONG : crossing is over
                    TRUE : over ;
                  esac ;
   -- move thing?
   next(cabbage-over) := next(takes) = cabbage ? ! cabbage-over : cabbage-over ;
   next(lamb-over) := next(takes) = lamb ? ! lamb-over : lamb-over ;
   next(wolf-over) := next(takes) = wolf ? ! wolf-over : wolf-over ;
   -- don't move farmer if over
   next(farmer-over) := next(takes) = over ? farmer-over : ! farmer-over ;
   
 INVARSPEC ! (farmer-over != cabbage-over & farmer-over != lamb-over) ;
 INVARSPEC ! (farmer-over != lamb-over & farmer-over != wolf-over) ;

 CTLSPEC ! (EF (farmer-over = TRUE & cabbage-over = farmer-over & lamb-over = farmer-over & wolf-over = farmer-over)) ;
